cmake_minimum_required(VERSION 3.20)

if(NOT DEFINED BUILD_TYPE)
  set(BUILD_TYPE Debug)
endif()

if(NOT DEFINED PR_NUMBER)
  set(PR_NUMBER "branch")
endif()

if(NOT DEFINED BUILD_DIR)
  message(FATAL_ERROR "BUILD_DIR must be defined")
endif()

if(NOT DEFINED TEST_INFO)
  set(TEST_INFO "build")
endif()

# --- CDash metadata ---
set(CTEST_SITE "${CTEST_SITE}")
set(CTEST_BUILD_CONFIGURATION ${BUILD_TYPE})
set(CTEST_BUILD_NAME "${PR_NUMBER}-${BUILD_ARCH}-${BUILD_TYPE}-${TEST_INFO}")

set(CTEST_SOURCE_DIRECTORY "$ENV{CI_PROJECT_DIR}")
set(CTEST_BINARY_DIRECTORY "${BUILD_DIR}")
set(CTEST_CMAKE_GENERATOR "Ninja")
set(CTEST_GROUP "Pull_Requests")
set(CTEST_GROUP "Experimental")

# --- start a new build in CDash ---
ctest_start(Experimental GROUP "${CTEST_GROUP}")

set(CTEST_CONFIGURE_COMMAND "${CMAKE_COMMAND}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -S${CTEST_SOURCE_DIRECTORY}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -B${CTEST_BINARY_DIRECTORY}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -G${CTEST_CMAKE_GENERATOR}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} --preset=${PRESET}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DCMAKE_BUILD_TYPE=${BUILD_TYPE}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DCMAKE_BUILD_RPATH_USE_ORIGIN=ON")
if(DEFINED IPPL_PLATFORMS)
  set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DIPPL_PLATFORMS=${IPPL_PLATFORMS}")
endif()
if(DEFINED IPPL_OPENMP_THREADS)
  set(CTEST_CONFIGURE_COMMAND
      "${CTEST_CONFIGURE_COMMAND} -DIPPL_OPENMP_THREADS=${IPPL_OPENMP_THREADS}")
endif()
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DIPPL_ENABLE_SOLVERS=ON")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DIPPL_MARK_FAILING_TESTS=ON")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DMPIEXEC_EXECUTABLE=${MPIEXEC_EXECUTABLE}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DMPIEXEC_PREFLAGS=${MPIEXEC_PREFLAGS}")
set(CTEST_CONFIGURE_COMMAND
    "${CTEST_CONFIGURE_COMMAND} -DMPIEXEC_MAX_NUMPROCS=${MPIEXEC_MAX_NUMPROCS}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -DKokkos_VERSION=${Kokkos_VERSION}")
set(CTEST_CONFIGURE_COMMAND "${CTEST_CONFIGURE_COMMAND} -D${Kokkos_ARCH_FLAG}=ON")

# --- configure & build ---
ctest_configure(RETURN_VALUE configure_result)
ctest_build(RETURN_VALUE build_result)

# --- fail if any test failed ---
# note that we don't submit, if all is ok, because the test phase will submit anyway
if(configure_result OR build_result)
  # submit configure + build results
  ctest_submit()
  # make sure to fail the build if configure or build failed
  message(FATAL_ERROR "CTest reported configure/build failures")
endif()
